/*Scenario 1*/

types
  enum PCA_Errors {
    InCorrectDrugAdministration,
    FailedToAdministerDrug,
    SpO2ValueHigh,
    SpO2ValueLow,
    SpO2Early,
    SpO2Late,
    NoSpO2,
    CmdOutOfSpec,
    MissingCmd,
    CmdEarly,
    CmdLate
  }

components
  PCA_Pump
    with PCA_Errors
      ports
        in control
        out infuse
      propagations
        control = {CmdOutOfSpec, MissingCmd, CmdEarly, CmdLate}
        infuse = {InCorrectDrugAdministration, FailedToAdministerDrug}
      flows
        Pump : control -> infuse
        SpontaneousError : * -> infuse{InCorrectDrugAdministration, FailedToAdministerDrug}
        BadCommand2Infuse : control{CmdOutOfSpec, MissingCmd, CmdEarly, CmdLate} ->
                             infuse{InCorrectDrugAdministration}
        NonDangers : control{CmdOutOfSpec, CmdLate, MissingCmd, CmdEarly} ->
                      infuse{FailedToAdministerDrug}

  PulseOx
    with PCA_Errors
      ports
        in Saturation
        out SpO2
      propagations
        SpO2 = {SpO2ValueHigh, SpO2ValueLow, SpO2Early, SpO2Late, NoSpO2}
      flows
        Read_SpO2 : Saturation -> SpO2
        SpO2Error : * -> SpO2{SpO2ValueHigh, SpO2ValueLow, SpO2Early, SpO2Late, NoSpO2}

  App
    with PCA_Errors
      ports
        in SpO2
        out Cmd
      propagations
        SpO2 = {SpO2ValueHigh, SpO2ValueLow, SpO2Early, SpO2Late, NoSpO2}
        Cmd = {CmdOutOfSpec, MissingCmd, CmdEarly, CmdLate}
      flows
        SpO2Late : SpO2{SpO2Late} -> Cmd{CmdLate}
        Check_Status : SpO2 -> Cmd
        NoSpO2 : SpO2{NoSpO2} ->Cmd{MissingCmd}
        corruptedApp : * -> Cmd{CmdOutOfSpec, MissingCmd, CmdEarly, CmdLate}
        SpO2Early : SpO2{SpO2Early} -> Cmd{CmdLate, MissingCmd}
        SpO2High : SpO2{SpO2ValueHigh} -> Cmd{CmdOutOfSpec}
        SpO2Low : SpO2{SpO2ValueLow} -> Cmd{CmdOutOfSpec}

  Patient
    with PCA_Errors
      ports
        in Vein
        out BloodSat
      propagations
        Vein = {InCorrectDrugAdministration, FailedToAdministerDrug}
      flows
        Absorb : Vein -> *
        BloodSaturation : * -> BloodSat

connections
  read: Patient.BloodSat -> PulseOx.Saturation

  spo2: PulseOx.SpO2 -> App.SpO2
   flows
      network_failure_pulseox_app: * -> {SpO2Late, NoSpO2}

  activate: App.Cmd -> PCA_Pump.control
   flows
      network_failure_app_pump: * -> {CmdLate, MissingCmd}

  administer: PCA_Pump.infuse -> Patient.Vein
